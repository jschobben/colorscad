find_package(PkgConfig)
if(PkgConfig_FOUND)
  pkg_check_modules(PC_LIB3MF lib3mf)
else()
  message(WARNING "pkgconfig is not installed, unable to check for system version of lib3mf")
endif()

if(PC_LIB3MF_FOUND)
  find_path(LIB3MF_INCLUDE_DIRS
    NAMES lib3mf_implicit.hpp
    PATHS ${PC_LIB3MF_INCLUDE_DIRS}
    PATH_SUFFIXES lib3mf Bindings/Cpp
  )

  find_library(LIB3MF_LIBRARIES
    NAMES ${PC_LIB3MF_LIBRARIES}
    PATHS ${PC_LIB3MF_LIBRARY_DIRS}
  )

  set(LIB3MF_VERSION ${PC_LIB3MF_VERSION})

  include(FindPackageHandleStandardArgs)
  find_package_handle_standard_args(LIB3MF
    FOUND_VAR LIB3MF_FOUND
    REQUIRED_VARS
      LIB3MF_LIBRARIES
      LIB3MF_INCLUDE_DIRS
    VERSION_VAR LIB3MF_VERSION
  )
endif()

if(LIB3MF_FOUND AND NOT TARGET lib3mf)
  add_library(lib3mf UNKNOWN IMPORTED)
  set_target_properties(lib3mf PROPERTIES
    IMPORTED_LOCATION "${LIB3MF_LIBRARIES}"
    INTERFACE_COMPILE_OPTIONS "${PC_LIB3MF_CFLAGS_OTHER}"
    INTERFACE_INCLUDE_DIRECTORIES "${LIB3MF_INCLUDE_DIRS}"
  )
else()
  set(LIB3MF_VERSION 2.4.1)
  set(LIB3MF_SOURCES_HASH 4e9e1776f4dd1b3dfce684ce9bb4ad1157dadf29908a1f3aabb6cd4358bf3248)
  message("Building LIB3MF ${LIB3MF_VERSION} from source")

  unset(LIB3MF_INCLUDE_DIRS)
  unset(LIB3MF_LIBRARIES)

  set(PATCHES
    ${CMAKE_CURRENT_SOURCE_DIR}/patches/lib3mf_static.patch
    ${CMAKE_CURRENT_SOURCE_DIR}/patches/lib3mf_missing_include.patch
  )

  FetchContent_Declare(
    lib3mf
    URL https://github.com/3MFConsortium/lib3mf/releases/download/v${LIB3MF_VERSION}/lib3mf-${LIB3MF_VERSION}-source-with-submodules.zip
    URL_HASH SHA256=${LIB3MF_SOURCES_HASH}
    PATCH_COMMAND git apply --ignore-whitespace ${PATCHES}
    EXCLUDE_FROM_ALL
  )
  # LIB3MF still uses "cmake_minimum_required (VERSION 3.0)", which is unsupported in CMake 4.0
  set(CMAKE_POLICY_VERSION_MINIMUM 3.5)

  FetchContent_MakeAvailable(lib3mf)

  set_target_properties(lib3mf PROPERTIES
    INTERFACE_INCLUDE_DIRECTORIES "${lib3mf_SOURCE_DIR}/Autogenerated/Bindings/Cpp"
  )
endif()
